apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file(free42_keystore)
            storePassword free42_keystorePassword
            keyAlias free42_key
            keyPassword free42_keyPassword
        }
    }
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 142
        versionName "3.0.7"
    }
    buildTypes {
        debug {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
            signingConfig signingConfigs.release
        }
        release {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
    ndkVersion "19.2.5345600"
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
    flavorDimensions "floatType"
    productFlavors {
        decimal {
            getIsDefault().set(true)
            dimension "floatType"
            applicationId "com.thomasokken.plus42"
            manifestPlaceholders = [appName:"Plus42"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=decimal"
                }
            }
        }
        binary {
            dimension "floatType"
            applicationId "com.thomasokken.plus42b"
            manifestPlaceholders = [appName:"Plus42 Binary"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=binary"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
